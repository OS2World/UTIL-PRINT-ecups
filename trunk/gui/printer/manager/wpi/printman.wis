<WARPIN VERSION="1.0.15 CODEPAGE=850">
<HEAD>
    <TITLE>PM Printer Manager</TITLE>

    <!-- REXX FUNCTIONS ----------------------------------------------------->

    <REXX NAME="DefaultPath">
        path = VALUE('OSDIR',,'OS2ENVIRONMENT')
        IF path == '' THEN RETURN '$(WARPIN_DEFAULTTOOLSPATH)'
        RETURN path'\system'
    </REXX>

    <REXX NAME="CheckDLL">
        /* Searches the system LIBPATH for the specified DLL.  If not found,
         * displays a popup dialog, and optionally aborts the installation.
         *
         * Parameters:
         *      dname   Name of the DLL to look for (must include extension)
         *      fail    If 1, abort installation if the DLL was not found
         *      message Message text to be shown when the DLL is not found
         */
        CALL RxFuncAdd 'SysLoadFuncs', 'REXXUTIL', 'SysLoadFuncs'
        CALL SysLoadFuncs

        PARSE ARG dname fail message
        IF dname == '' THEN RETURN ''

        _bd = SysBootDrive()
        IF _bd == '' THEN
            _bd = FILESPEC('DRIVE', VALUE('COMSPEC',,'OS2ENVIRONMENT'))
        _csys = _bd'\CONFIG.SYS'

        CALL SysFileSearch 'LIBPATH=', _csys, 'match.'
        DO i = 1 TO match.0
            PARSE UPPER VAR match.i _lead 'LIBPATH=' _lpath
            IF (_lead == '') & (_lpath <> '') THEN LEAVE
        END
        CALL SETLOCAL
        _testenv = 'TEST_'dname'_LIBPATH'
        CALL VALUE _testenv, _lpath, 'OS2ENVIRONMENT'
        location = SysSearchPath( _testenv, dname )
        CALL ENDLOCAL

        IF location == '' THEN DO
            title = dname 'Not Found'
            rc = WirexxShowMessage( title, message )
            IF fail == 1 THEN CALL WirexxCancel
        END

        RETURN ''
    </REXX>

    <!-- PACKAGES ----------------------------------------------------------->

    <PCK INDEX=1
        PACKAGEID="Alex Taylor\Printer Manager\Program\0\5\0"
        TITLE="Printer Manager (including English language support)"
        TARGET="=("DefaultPath")\printman"
        SELECT

         =("CheckDLL VROBJ.DLL 0 The runtime library VROBJ.DLL was not found on your system. You must install this library in order to use Printer Manager.")
         =("CheckDLL VROBJEX.DLL 0 The runtime library VROBJEX.DLL was not found on your system. You must install this library in order to use Printer Manager.")
         =("CheckDLL WPTOOLS.DLL 0 The runtime library WPTOOLS.DLL was not found on your system. You must install this library in order to use Printer Manager.")
         =("CheckDLL PR1UTIL.DLL 0 The runtime library PR1UTIL.DLL was not found on your system. You must install this library in order to use Printer Manager.")

        CREATEOBJECT="REPLACE WPProgram|Manage Printers|<WP_PRINTERSFOLDER>|EXENAME=$(1)\printman.exe;PROGTYPE=PM;OBJECTID=<PRINT_MANAGER>;"
        CREATEOBJECT="REPLACE WPProgram|Install Printer|<WP_PRINTERSFOLDER>|EXENAME=$(1)\printman.exe;PARAMETERS=/create;PROGTYPE=PM;ICONFILE=$(1)\newprint.ico;OBJECTID=<ECS_PRINTGUIDE>;"
     >This package contains the graphical printer manager along with the English language files.</PCK>

</HEAD>

<BODY>

   <PAGE INDEX=1 TYPE=README>
    <NEXTBUTTON TARGET=2>~Next</NEXTBUTTON>
    <TEXT>PM Printer Manager is a GUI utility for managing printers and drivers.
    </TEXT>
    <README EXTRACTFROMPCK="1" format=PLAIN>readme.txt</README>
   </PAGE>

   <PAGE INDEX=2 TYPE=CONTAINER>
    <NEXTBUTTON TARGET=3>~Next</NEXTBUTTON>
    <TEXT>Select the components to install.
    </TEXT>
   </PAGE>

   <PAGE INDEX=3 TYPE=CONFIGURE>
    <TEXT>Ready to install.</TEXT>
    <NEXTBUTTON TARGET=0>I~nstall</NEXTBUTTON>
   </PAGE>

</BODY>
</WARPIN>
